require! {
  "./crawler": {
    MovieCrawler
  }
  "./persistence": {
    addMovie
    smartRandomMovieOne
    ignoreMovie
    cleanIgnoreMovie
  }
  "./log": {
    logWarning
    logError
    logInfo
    logSuccess
  }
  "cli-spinner":{
    Spinner
  }
  async
  prompt
}


mc = new MovieCrawler

execGrap = (cb = ->) ->
  mc.grab (movieList, next)->
    async.eachSeries movieList, (movie, cb) ->
      err <~ addMovie movie
      if err
        logError "æŒä¹…åŒ–<<#{movie.title}>>å¤±è´¥"
      else
        logInfo "æŒä¹…åŒ–<<#{movie.title}>>æˆåŠŸ"
      cb!
    , next
  , cb


execSmartRandomMovie = (opts) ->
  console.log "\r"
  spinner = new Spinner "  ğŸ¥  æ‰¾ç”µå½±å‘€æ‰¾ç”µå½± %s ".bold.green
  spinner.setSpinnerString 'â ‹â ™â ¹â ¸â ¼â ´â ¦â §â ‡â '
  spinner.start!

  err, data <- smartRandomMovieOne opts
  spinner.stop!
  if err or not data.movie?
    return console.log "\r\næœªæ‰¾åˆ°ä»»ä½•ç”µå½±!!".bold.red
  {
    movie
    count
  } = data
  downloadURLStr = ""
  movie.downloadURL.forEach (v) ->
    downloadURLStr += "  #{v}".underline.bold+"\r\n"

  desc = """\r
    ç‰‡å: #{movie.title.bold.blue}\r
    åˆ†ç±»: #{movie.classification}
    è¯„åˆ†: #{(movie.rank + "").bold.red}
    è¯¦ç»†: #{movie.link.underline.bold}\r\n
    ä¸‹è½½é“¾æ¥:\r
    #{downloadURLStr}\r\n
    è¯¥ç±»ç›®ä¸‹å…±è®¡: (#{count}) ä¸ªèµ„æº\r\n
  """
  console.log desc

  prompt.message = "ğŸ¥"
  prompt.start!
  err, result <- prompt.get {
    properties:
      seen:
        description: "çœ‹è¿‡äº†? y/n"
  }
  return if err
  {seen} = result
  if seen is "y"
    ignoreMovie movie.id

execClean = ->
  cleanIgnoreMovie!
  console.log "æ¸…é™¤å®Œæ¯•!".green

module.exports = {
  execGrap
  execSmartRandomMovie
  execClean
}
